apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"
    defaultConfig {
        applicationId "com.lunarg.renegade.wsiwindow"
        minSdkVersion 23
        targetSdkVersion 24
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11"
                abiFilters "armeabi-v7a", "arm64-v8a"
                arguments "-DANDROID_STL=gnustl_static"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {  // Adds options to:  Build -> Select Build Variant
        Validate{}    // Adds Vulkan Validation Layers to APK (19 MB)
        noValidate{}  // Skips Vulkan Validation Layers, for a smaller, faster APK
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'src/AndroidManifest.xml'
            java.srcDirs = ['src/java']
            res.srcDirs = ['src/res']
            assets.srcDirs = ['assets','src/assets','../../assets']
        }
        Validate{   // Use Build -> Select Build Variant : ValidateDebug to enable Validation layers
            jniLibs {
                srcDir "$android.ndkDirectory/sources/third_party/vulkan/src/build-android/jniLibs"  // Path to Vulkan Validation Layers
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}
